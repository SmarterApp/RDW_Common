buildscript {
  repositories {
    mavenLocal()
    jcenter()
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  dependencies {
    classpath("io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE")
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
    classpath('org.springframework.build.gradle:propdeps-plugin:0.0.7')
  }
}

apply plugin: 'io.spring.dependency-management'

allprojects {
    group = 'org.opentestsystem.rdw.common'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
}

subprojects {
  apply plugin: "java"
  apply plugin: "maven"
  apply plugin: "jacoco"
  apply plugin: 'io.spring.dependency-management'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
      mavenCentral()
  }

  dependencyManagement {
    dependencies {
      dependency 'com.google.guava:guava:21.0'
    }
    imports {
      // make springframework versions consistent with other projects in the RDW suite
      mavenBom 'org.springframework.boot:spring-boot-dependencies:1.5.2.RELEASE'
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR5'
    }
  }

  dependencies {
    compile 'org.slf4j:slf4j-api:1.7+'

    testCompile 'junit:junit:4.12+'
    testCompile 'org.mockito:mockito-core:1.10+'
    testCompile 'org.assertj:assertj-core:3.6+'
  }

  jacoco {
    toolVersion = "0.7.6.201602180812"
  }
  test.finalizedBy jacocoTestReport
}
  

